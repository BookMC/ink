plugins {
    id "com.github.johnrengelman.shadow" version "7.0.0"
    id "maven-publish"
    id "java-gradle-plugin"
    id "java"
}

repositories {
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
    maven {
        url "https://maven.minecraftforge.net"
    }
}

ext {
    def env = System.getenv()
    local = !env.GITHUB_ACTIONS
    jdk = JavaVersion.current().getMajorVersion()
}

group = "org.bookmc"
// We add the suffix for (our) non-lts support.
// Java 8 support will be removed in the future however.
version "1.2.1${local ? ".local" : ""}${JavaVersion.current() != JavaVersion.VERSION_1_8 ? "+jdk.$jdk" : ""}"

configurations {
    include
    implementation.extendsFrom(include)
}

dependencies {
    include("com.github.BookMC:ForgeGradle:91a4d3e4f4") {
        exclude module: "gson"
    }
    include "com.google.code.gson:gson:2.8.7"
    implementation gradleApi()
}

gradlePlugin {
    plugins {
        ink {
            id = "org.bookmc.ink"
            implementationClass = "org.bookmc.ink.InkPlugin"
        }
    }
}

shadowJar {
    configurations = [project.configurations.include]
    archiveClassifier.set("")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.build.dependsOn(tasks.shadowJar)

publishing {
    repositories {
        maven {
            def env = System.getenv()

            boolean isEnvVarsAvailable = env.containsKey("MAVEN_USER") && env.containsKey("MAVEN_PASSWORD")

            if (isEnvVarsAvailable) {
                credentials {
                    username env.get("MAVEN_USER")
                    password env.get("MAVEN_PASSWORD")
                }
            }

            url = !isEnvVarsAvailable ? "$buildDir/repository" : "https://maven.bookmc.org/releases/"
        }
    }

    publications {
        //noinspection GroovyAssignabilityCheck
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                url = "https://github.com/BookMC/${project.name}"

                developers {
                    developer {
                        name = "ChachyDev"
                    }
                }
            }
        }
    }
}