plugins {
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "maven-publish"
    id "java-gradle-plugin"
    id "java"
}

repositories {
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
    maven {
        url "https://maven.minecraftforge.net"
    }
}

group = "org.bookmc"
version = "1.2"

configurations {
    include
    implementation.extendsFrom(include)
}

dependencies {
    include("com.github.BookMC:ForgeGradle:91a4d3e4f4") {
        exclude module: "gson"
    }
    include "com.google.code.gson:gson:2.8.7"
    implementation gradleApi()
}

gradlePlugin {
    plugins {
        ink {
            id = "org.bookmc.ink"
            implementationClass = "org.bookmc.ink.InkPlugin"
        }
    }
}

shadowJar {
    configurations = [project.configurations.include]
    archiveClassifier.set("")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.build.dependsOn(tasks.shadowJar)

publishing {
    repositories {
        maven {
            url = "$buildDir/repository"
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = "ink"
                url = "https://github.com/BookMC/ink"

                developers {
                    developer {
                        name = "BookMC"
                    }
                }
            }
        }
    }
}